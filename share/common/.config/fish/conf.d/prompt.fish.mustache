function green
  set_color -o {{base0B-hex}}
end

function yellow
  set_color -o {{base0A-hex}}
end

function blue
  set_color -o {{base0D-hex}}
end

function magenta
  set_color -o {{base0F-hex}}
end

function red
  set_color -o {{base08-hex}}
end

function cyan
  set_color -o {{base0C-hex}}
end

function white
  set_color -o {{base05-hex}}
end

function dim
  set_color -o {{base03-hex}}
end

function off
  set_color -o normal
end

function git::is_repo
  test -d .git; or command git rev-parse --git-dir >/dev/null 2>/dev/null
end

function git::ahead -a ahead behind diverged none
  not git::is_repo; and return

  set -l commit_count (command git rev-list --count --left-right "@{upstream}...HEAD" 2>/dev/null)

  switch "$commit_count"
  case ""
  case "0"\t"0"
    test -n "$none"; and echo "$none"; or echo ""
  case "*"\t"0"
    test -n "$behind"; and echo "$behind"; or echo "-"
  case "0"\t"*"
    test -n "$ahead"; and echo "$ahead"; or echo "+"
  case "*"
    test -n "$diverged"; and echo "$diverged"; or echo "Â±"
  end
end

function git::branch_name
  git::is_repo; and begin
    command git symbolic-ref --short HEAD 2>/dev/null;
    or command git show-ref --head -s --abbrev | head -n1 2>/dev/null
  end
end

function git::is_dirty
  git::is_repo; and not command git diff --no-ext-diff --quiet --exit-code
end

function git::is_staged
  git::is_repo; and begin
    not command git diff --cached --no-ext-diff --quiet --exit-code
  end
end

function git::is_stashed
  git::is_repo; and begin
    command git rev-parse --verify --quiet refs/stash >/dev/null
  end
end

function git::is_touched
  git::is_repo; and begin
    test -n (echo (command git status --porcelain))
  end
end

function git::untracked
  git::is_repo; and begin
    command git ls-files --other --exclude-standard
  end
end

function nix::is_in_shell
  test "$IN_NIX_SHELL" != ""
end

function fish_right_prompt
  set cwd (prompt_pwd)
  set timestamp (date +%H%M%S)

  if git::is_repo
    set root_folder (command git rev-parse --show-toplevel 2>/dev/null)
    set parent_root_folder (dirname $root_folder)
    set cwd (echo $PWD | sed -e "s|$parent_root_folder/||")
  end

  printf (dim)"$cwd | $timestamp"(off)
end

function fish_prompt
  set -q SYMBOL || set SYMBOL '$'
  set -l code $status

  if test -n "$ssh_client"
    set -l host (hostname -s)
    set -l who (whoami)
    echo -n -s (red)"("(cyan)"$who"(red)":"(cyan)"$host"(red)") "(off)
  end

  if git::is_repo
    set -l branch (git::branch_name 2>/dev/null)
    set -l ref (git show-ref --head --abbrev | awk '{print substr($0,0,7)}' | sed -n 1p)

    if git::is_stashed
      echo -n -s (white)"^"(off)
    end

    echo -n -s (red)"("(off)

    if git::is_dirty
      printf (white)"*"(off)
    end

    if command git symbolic-ref HEAD > /dev/null 2>/dev/null
      if git::is_staged
        printf (cyan)"$branch"(off)
      else
        printf (yellow)"$branch"(off)
      end
    else
      printf (dim)"$ref"(off)
    end

    for remote in (git remote)
      set -l behind_count (echo (command git rev-list $branch..$remote/$branch 2>/dev/null | wc -l | tr -d " "))
      set -l ahead_count (echo (command git rev-list $remote/$branch..$branch 2>/dev/null | wc -l | tr -d " "))

      if test $ahead_count -ne 0; or test $behind_count -ne 0; and test (git remote | wc -l) -gt 1
        echo -n -s " "(yellow)$remote(off)
      end

      if test $ahead_count -ne 0
        echo -n -s (white)" +"$ahead_count(off)
      end

      if test $behind_count -ne 0
        echo -n -s (white)" -"$behind_count(off)
      end
    end

    echo -n -s (red)") "(off)
  end

  if nix::is_in_shell
    echo -n -s (dim)"nix "(off)
  end

  if test "$code" = 0
    echo -n -s (green)"$SYMBOL "(off)
  else
    echo -n -s (red)"$SYMBOL:$code"(off)
  end
end
